.template 0
#   Copyright (C) 2015-2018 Virgil Security Inc.
#
#   All rights reserved.
#
#   Redistribution and use in source and binary forms, with or without
#   modification, are permitted provided that the following conditions are
#   met:
#
#       (1) Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#
#       (2) Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in
#       the documentation and/or other materials provided with the
#       distribution.
#
#       (3) Neither the name of the copyright holder nor the names of its
#       contributors may be used to endorse or promote products derived from
#       this software without specific prior written permission.
#
#   THIS SOFTWARE IS PROVIDED BY THE AUTHOR ''AS IS'' AND ANY EXPRESS OR
#   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
#   WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
#   DISCLAIMED. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT,
#   INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
#   (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
#   SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
#   HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
#   STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
#   IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
#   POSSIBILITY OF SUCH DAMAGE.
#
#   Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>

# ---------------------------------------------------------------------------
#   Create module with 'Interface Private API'.
#   Private function prefix is: B25.
# ---------------------------------------------------------------------------
#   This is a code generator helper built using the iMatix GSL code
#   generation language. See https://github.com/imatix/gsl for details.
# ---------------------------------------------------------------------------

gsl from "common.gsl"
gsl from "component.gsl"


function _B25_add_interface_api_type (interface, destination, meta)
    check_arguments (my, "interface, destination, meta", my)

    my.names = meta_names (my.meta)
    my.refs = meta_refs (my.meta)

    new struct to my.destination
        . = "Handles constants and callbacks of the interface '$(my.interface.name:)'."
        .name = my.names.class_interface_api
        .visibility = "public"
        .declaration = "external"
        .definition = "public"

        new property to struct
            . = "\
                API's unique identifier, MUST be first in the structure.
                For interface '$(my.interface.name:c)' MUST be equal to the \
                '$(my.refs.c_interface_constant_tag)'."

            .name = my.names.property_api_tag
            .enum = my.refs.class_api_enum_tag
            .value = my.refs.interface_constant_tag
            .tag = "tag"
        endnew

        for my.interface.inherit
            assert_attribute (inherit, "interface", my)

            new property to struct
                . = "Link to the inherited interface API '$(inherit.interface:)'."
                .name = my.names.property_inherited_$(make_id(inherit.interface))_api
                .class = my.refs.class_inherited_$(make_id(inherit.interface))_api
                .tag = "api"
            endnew
        endfor

        for my.interface.method
            new property to struct
                . = method. ?
                .name = method.name
                .callback = my.refs.interface_callback_$(make_id (method.name))
                .tag = "method"
            endnew

            my.callback = item_copy_to (method, my.destination, "callback")
            my.callback. = ("Callback. " + string_trim(my.callback.)) ?

            if method.context = "impl"
                component_create_argument_impl (my.callback)
            endif
        endfor

        for my.interface.constant
            my.property = item_copy_to (constant, struct, "property")
            my.property.tag = "constant"
        endfor
    endnew

endfunction

# ---------------------------------------------------------------------------
#   Entrypoint. Create module with 'Interface API'.
# ---------------------------------------------------------------------------
function c_module_interface_api_create (source, destination, meta)
    check_arguments (my, "source, destination, meta", my)

    for my.source.interface
        meta_reset_active_entity (my.meta, interface.name)

        my.names = meta_names (my.meta)

        new module to my.destination
            . = "Interface '$(interface.name:)' API."
            .name = my.names.module_interface_api
            .class = my.names.class_interface_api
            .scope = "private"

            new require to module
                .module = my.names.module_library
            endnew

            new require to module
                .module = my.names.module_api
            endnew

            new require to module
                .module = my.names.module_impl
            endnew

            for interface.inherit
                new require to module
                    .module = inherit.interface
                endnew
            endfor


            _B25_add_interface_api_type (interface, module, my.meta)

            for module.method
                c_method_add_preconditions (method)
            endfor
        endnew
    endfor
endfunction


# ---------------------------------------------------------------------------
#   Return 'module' that contains 'Interface API type'.
# ---------------------------------------------------------------------------
function c_module_interface_api_find (name, source, meta)
    check_arguments (my, "name, source, meta", my)

    meta_activate_entity (my.meta, my.name)
    my.names = meta_names (my.meta)

    for my.source.module
        my.api_type = module->struct (struct.name = my.names.class_interface_api & struct.definition <> "external") ?

        if defined (my.api_type)
            meta_deactivate_entity (my.meta, my.name)
            return module
        endif
    endfor

    meta_deactivate_entity (my.meta, my.name)

    echo_fatal ("API type of the interface '$(my.name)'" + \
            " is not found within given scope.", my)
endfunction

# ---------------------------------------------------------------------------
#   Return 'struct' that describes 'Interface API type'.
# ---------------------------------------------------------------------------
function c_module_interface_api_find_type (name, source, meta)
    check_arguments (my, "name, source, meta", my)

    meta_activate_entity (my.meta, my.name)
    my.names = meta_names (my.meta)

    my.interface_api_module = c_module_interface_api_find (my.name, my.source, my.meta)
    assert_attribute (my, "interface_api_module", my)

    my.api_type = my.interface_api_module->struct (struct.name = my.names.class_interface_api) ?

    assert_attribute (my, "api_type", my)

    meta_deactivate_entity (my.meta, my.name)

    return my.api_type
endfunction

.endtemplate
