.template 0
#   Copyright (C) 2015-2018 Virgil Security Inc.
#
#   All rights reserved.
#
#   Redistribution and use in source and binary forms, with or without
#   modification, are permitted provided that the following conditions are
#   met:
#
#       (1) Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#
#       (2) Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in
#       the documentation and/or other materials provided with the
#       distribution.
#
#       (3) Neither the name of the copyright holder nor the names of its
#       contributors may be used to endorse or promote products derived from
#       this software without specific prior written permission.
#
#   THIS SOFTWARE IS PROVIDED BY THE AUTHOR ''AS IS'' AND ANY EXPRESS OR
#   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
#   WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
#   DISCLAIMED. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT,
#   INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
#   (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
#   SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
#   HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
#   STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
#   IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
#   POSSIBILITY OF SUCH DAMAGE.
#
#   Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>

# ---------------------------------------------------------------------------
#   This module contains functions processing component 'project'.
# ---------------------------------------------------------------------------
#   This is a code generator built using the iMatix GSL code generation
#   language. See https://github.com/imatix/gsl for details.
# ---------------------------------------------------------------------------


gsl from "common.gsl"

# ---------------------------------------------------------------------------
#   Add missed attributes and components to the 'project'.
# ---------------------------------------------------------------------------
function project_resolve (project)
    check_argument (my, "project")

    if count (my.project.version) = 0
        new version to my.project
        endnew
    endif

    assert_item (my.project, "version", my)
    assert_attribute (my.project, "path", my)
    assert_attribute (my.project, "inc_path", my)
    assert_attribute (my.project, "inc_private_path", my)
    assert_attribute (my.project, "src_path", my)
    assert_attribute (my.project, "work_path", my)

    my.version = my.project->version
    my.version.major ?= 1
    my.version.minor ?= 0
    my.version.patch ?= 0

    my.project.version_major = my.version.major
    my.project.version_minor = my.version.minor
    my.project.version_patch = my.version.patch

    my.project.version_string = "$(my.version.major:).$(my.version.minor:).$(my.version.patch:)"
    my.project.cmake_target ?= "$(my.project.name:c)"
endfunction

# ---------------------------------------------------------------------------
#   Generate project structure: folders, build files, etc.
# ---------------------------------------------------------------------------
function project_generate_structure (project)
    check_argument (my, "project")

    directory.create (my.project.path)
    directory.create (my.project.inc_path)
    directory.create (my.project.inc_private_path)
    directory.create (my.project.src_path)
    directory.create (my.project.work_path)
endfunction

# ---------------------------------------------------------------------------
#   Update resolution context with a project information.
# ---------------------------------------------------------------------------
function project_update_context (project, context)
    check_argument (my, "project")
    check_argument (my, "context")

    my.context.project_version_major = my.project.version_major
    my.context.project_version_minor = my.project.version_minor
    my.context.project_version_patch = my.project.version_patch
    my.context.project_version_string = my.project.version_string
endfunction

# ---------------------------------------------------------------------------
#   Add 'features' from project to destination.
# ---------------------------------------------------------------------------
function project_create_features (project, destination)
    check_arguments (my, "project, destination", my)

    echo_trace ("Create <features/> for <project name=\"$(my.project.name)\"", my)

    new features to my.destination
        .prefix = my.project.prefix ?
        .path = my.project.path

        new source to features
            .name = my.project.name
            .type = "project"
        endnew

        for my.project.feature
            copy feature to features
        endfor

        return features
    endnew
endfunction

.endtemplate
