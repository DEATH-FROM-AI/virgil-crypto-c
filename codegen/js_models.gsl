.template 0

gsl from "common.gsl"
gsl from "js_helpers.gsl"

# --------------------------------------------------------------------------------------------------
# General
# --------------------------------------------------------------------------------------------------

function js_append_js_module_model(parent, source)
  new js_module to my.parent
    js_module.name = my.source.name
    return js_module
  endnew
endfunction

function js_append_js_interface_module_model(parent, source, wrapper)
  new js_interface_module to my.parent
    js_interface_module.name = my.source.name
    js_interface_module.class_name = "$(my.source.name:Pascal)"
    js_interface_module.implementation_type = "$(my.wrapper.prefix:no)_impl_t"
    js_interface_module.implementation_getter = "GetImplementation"
    return js_interface_module
  endnew
endfunction

function js_append_js_wrapped_interface_model(parent, js_interface_module, wrapper)
  new js_wrapped_interface to my.parent
    js_wrapped_interface.name = my.js_interface_module.name
    js_wrapped_interface.class_name = my.js_interface_module.class_name
    js_wrapped_interface.cpp_header_file = "$(my.js_interface_module.name:c).h"
    js_wrapped_interface.cpp_header_file_path = cat_path(my.wrapper.project_source_dir, js_wrapped_interface.cpp_header_file)
    return js_wrapped_interface
  endnew
endfunction

function js_append_js_implementation_module_model(parent, source, wrapper)
  new js_implementation_module to my.parent
    js_implementation_module.name = my.source.name
    for my.source.interface
      new js_interface_implementation to js_implementation_module
        js_interface_implementation.interface_name = interface.name
      endnew
      for interface.constant
        copy constant to js_implementation_module
      endfor
      for interface.method
        copy method to js_implementation_module
      endfor
    endfor
    for my.source.constant
      copy constant to js_implementation_module
    endfor
    for my.source.method
      copy method to js_implementation_module
    endfor
    return js_implementation_module
  endnew
endfunction

function js_append_js_wrapped_implementation_model(parent, js_implementation_module, wrapper)
  new js_wrapped_implementation to my.parent
    js_wrapped_implementation.name = my.js_implementation_module.name
    return js_wrapped_interface
  endnew
endfunction

function js_append_js_enum_module_model(parent, source)
  new js_enum_module to my.parent
    js_enum_module.name = my.source.name
    js_enum_module.variable_name = "$(my.source.name:Pascal)"
    for my.source.constant
      js_append_js_enum_value_model(js_enum_module, constant, index(constant) - 1)
    endfor
    return js_enum_module
  endnew
endfunction

function js_append_js_wrapped_enum_model(parent, js_enum_module, wrapper)
  new js_wrapped_enum to my.parent
    js_wrapped_enum.name = my.js_enum_module.name
    js_wrapped_enum.c_name = "$(my.wrapper.prefix)_$(my.js_enum_module.name:c)_t"
    js_wrapped_enum.js_source_file = "$(my.js_enum_module.name:cobol).js"
    js_wrapped_enum.js_source_file_path = cat_path(my.wrapper.project_source_dir, js_wrapped_enum.js_source_file)
    return js_wrapped_enum
  endnew
endfunction

function js_append_js_class_module_model(parent, source, wrapper)
  new js_class_module to my.parent
    js_class_module.name = my.source.name
    js_append_js_class_model(js_class_module, my.source)
    if count(my.source.method) > 0
      js_append_cpp_class_model(js_class_module, my.source, my.wrapper)
    endif
    return js_class_module
  endnew
endfunction

function js_append_js_wrapped_class_model(parent, js_class_module, wrapper)
  new js_wrapped_class to my.parent
    js_wrapped_class.name = my.js_class_module.name
    js_wrapped_class.js_class_name = my.js_class_module->js_class.name
    js_wrapped_class.js_source_file = "$(my.js_class_module.name:cobol).js"
    js_wrapped_class.js_source_file_path = cat_path(my.wrapper.project_source_dir, js_wrapped_class.js_source_file)
    if defined(my.js_class_module->cpp_class)
      js_wrapped_class.cpp_class_name = my.js_class_module->cpp_class.name
      js_wrapped_class.cpp_context_name = my.js_class_module->cpp_class.context_name
      js_wrapped_class.cpp_header_file = "$(my.js_class_module.name:c).h"
      js_wrapped_class.cpp_header_file_path = cat_path(my.wrapper.project_source_dir, js_wrapped_class.cpp_header_file)
      js_wrapped_class.cpp_source_file = "$(my.js_class_module.name:c).cc"
      js_wrapped_class.cpp_source_file_path = cat_path(my.wrapper.project_source_dir, js_wrapped_class.cpp_source_file)
    endif
    return js_wrapped_class
  endnew
endfunction

function js_append_js_include_module_model(parent, js_wrapped_class)
  new js_include_module to my.parent
    js_include_module.name = my.js_wrapped_class.name
    js_include_module.js_class_name = my.js_wrapped_class.js_class_name
    js_include_module.js_source_file = my.js_wrapped_class.js_source_file
    js_include_module.cpp_class_name = my.js_wrapped_class.cpp_class_name
    js_include_module.cpp_header_file = my.js_wrapped_class.cpp_header_file
    return js_include_module
  endnew
endfunction

function js_append_interfaces_to_js_class_module(js_class_module, js_implementation_module)
  if defined(my.js_class_module->cpp_class)
    for my.js_implementation_module.js_interface_implementation
      new cpp_interface_implementation to my.js_class_module->cpp_class
        cpp_interface_implementation.interface_name = js_interface_implementation.interface_name
      endnew
    endfor
  endif
endfunction

# --------------------------------------------------------------------------------------------------
# JavaScript
# --------------------------------------------------------------------------------------------------

function js_append_js_enum_value_model(parent, source_enum_constant, index)
  new js_enum_value to my.parent
    js_enum_value.name = "$(my.source_enum_constant.name:c,upper)"
    if defined(my.source_enum_constant.value)
      js_enum_value.value = my.source_enum_constant.value
    else
      js_enum_value.value = my.index
    endif
  endnew
endfunction

function js_append_js_constant_model(parent, source_constant)
  new js_constant to my.parent
    js_constant.name = "$(my.source_constant.name:c,upper)"
    js_constant.value = my.source_constant.value
    return js_constant
  endnew
endfunction

function js_append_js_argument_model(parent, source_argument)
  new js_argument to my.parent
    js_argument.name = "$(my.source_argument.name:Camel)"
    if defined(my.source_argument.type)
      js_argument.type = my.source_argument.type
    endif
    if defined(my.source_argument.enum)
      js_argument.enum = my.source_argument.enum
    endif
    if defined(my.source_argument.class)
      js_argument.class = my.source_argument.class
    endif
    if defined(my.source_argument.interface)
      js_argument.interface = my.source_argument.interface
    endif
    return js_argument
  endnew
endfunction

function js_append_js_return_model(parent, source_return)
  new js_return to my.parent
    if defined(my.source_return.type)
      js_return.type = my.source_return.type
    endif
    if defined(my.source_return.enum)
      js_return.enum = my.source_return.enum
    endif
    if defined(my.source_return.class)
      js_return.class = my.source_return.class
    endif
    return js_return
  endnew
endfunction

function js_append_method_model(parent, source_method)
  new js_method to my.parent
    js_method.name = "$(my.source_method.name:Camel)"
    js_method.binding_name = js_binding_method_name(my.source_method)
    if defined(my.source_method.is_static)
      js_method.is_static = my.source_method.is_static
    endif
    for my.source_method.argument where js_source_argument_is_js_argument(argument)
      js_append_js_argument_model(js_method, argument)
    endfor
    if defined(my.source_method->return)
      js_append_js_return_model(js_method, my.source_method->return)
    endif
    return js_method
  endnew
endfunction

function js_append_js_class_model(parent, source)
  new js_class to my.parent
    js_class.name = "$(my.source.name:Pascal)"
    js_class.binding_name = js_binding_class_name(js_class)
    for my.source.constant where !js_constant_is_private(constant)
      js_append_js_constant_model(js_class, constant)
    endfor
    for my.source.method where !js_method_is_private(method)
      js_append_method_model(js_class, method)
    endfor
    return js_class
  endnew
endfunction

# --------------------------------------------------------------------------------------------------
# C++
# --------------------------------------------------------------------------------------------------

function js_append_cpp_call_argument_model(parent, source_argument)
  new cpp_call_argument to my.parent
    cpp_call_argument.name = js_cpp_variable_name(my.source_argument.name)
    return cpp_call_argument
  endnew
endfunction

function js_append_cpp_size_argument_model(parent, source_argument)
  new cpp_size_argument to my.parent
    cpp_size_argument.name = js_cpp_variable_name(my.source_argument.name)
    return cpp_size_argument
  endnew
endfunction

function js_append_cpp_data_argument_model(parent, source_argument)
  new cpp_data_argument to my.parent
    cpp_data_argument.name = js_cpp_variable_name(my.source_argument.name)
    return cpp_data_argument
  endnew
endfunction

function js_append_cpp_call_argument_model_from_source_proxy(parent, source_proxy)
  new cpp_call_argument to my.parent
    if defined(my.source_proxy.argument)
      cpp_call_argument.name = js_cpp_variable_name(my.source_proxy.argument)
    endif
    if defined(my.source_proxy.constant)
      cpp_call_argument.constant = my.source_proxy.constant
    endif
    if defined(my.source_proxy.cast)
      cpp_call_argument.cast = my.source_proxy.cast
    endif
    return cpp_call_argument
  endnew
endfunction

function js_append_cpp_buffer_size_model(parent, name, source_length, cpp_class, wrapper)
  new cpp_buffer_size to my.parent
    if defined(my.source_length.argument)
      cpp_buffer_size.name = my.source_length.argument
    else
      cpp_buffer_size.name = js_cpp_size_name(my.name)
      if defined(my.source_length.constant)
        cpp_buffer_size.type = global.js_cpp_size_type_constant
        cpp_buffer_size.constant = "$(my.wrapper.prefix:no)_$(my.cpp_class.c_name:no)_$(my.source_length.constant:c,upper)"
      elsif defined(my.source_length.method)
        cpp_buffer_size.type = global.js_cpp_size_type_method
        cpp_buffer_size.method = "$(my.wrapper.prefix:no)_$(my.cpp_class.c_name:no)_$(my.source_length.method:c)"
        for my.source_length.proxy
          js_append_cpp_call_argument_model_from_source_proxy(cpp_buffer_size, proxy)
        endfor
      else
        echo_fatal("'source_length.constant' or 'source_length.method' should be defined", my)
      endif
    endif
    return cpp_buffer_size
  endnew
endfunction

function js_append_cpp_buffer_argument_model(parent, source_argument, cpp_class, wrapper)
  new cpp_buffer_argument to my.parent
    cpp_buffer_argument.name = js_cpp_variable_name(my.source_argument.name)
    if defined(my.source_argument->length)
      js_append_cpp_buffer_size_model(cpp_buffer_argument, cpp_buffer_argument.name, my.source_argument->length, my.cpp_class, my.wrapper)
    else
      echo_fatal("'source_argument->length' should be defined", my)
    endif
    return cpp_buffer_argument
  endnew
endfunction

function js_append_cpp_context_argument_model(parent, source_argument)
  new cpp_context_argument to my.parent
    cpp_context_argument.name = js_cpp_variable_name(my.source_argument.name)
    cpp_context_argument.module_name = my.source_argument.class
    return cpp_context_argument
  endnew
endfunction

function js_append_cpp_error_context_argument_model(parent, source_argument, wrapper)
  new cpp_error_context_argument to my.parent
    cpp_error_context_argument.name = js_cpp_variable_name(my.source_argument.name)
    cpp_error_context_argument.class = "$(my.wrapper.prefix:no)_$(my.source_argument.class:c)"
    return cpp_error_context_argument
  endnew
endfunction

function js_append_cpp_enum_argument_model(parent, source_argument)
  new cpp_enum_argument to my.parent
    cpp_enum_argument.name = js_cpp_variable_name(my.source_argument.name)
    cpp_enum_argument.enum_name = my.source_argument.enum
    return cpp_enum_argument
  endnew
endfunction

function js_append_cpp_char_argument_model(parent, source_argument)
  new cpp_char_argument to my.parent
    cpp_char_argument.name = js_cpp_variable_name(my.source_argument.name)
    if defined(my.source_argument->string)
      if defined(my.source_argument->string.length_constant)
        cpp_char_argument.length = my.source_argument->string.length_constant
      endif
      return cpp_char_argument
    else
      echo_fatal("'source_argument->string' should be defined", my)
    endif
  endnew
endfunction

function js_append_cpp_interface_argument_model(parent, source_argument)
  new cpp_interface_argument to my.parent
    cpp_interface_argument.name = js_cpp_variable_name(my.source_argument.name)
    cpp_interface_argument.interface_name = my.source_argument.interface
    return cpp_interface_argument
  endnew
endfunction

function js_append_cpp_byte_argument_model(parent, source_argument)
  new cpp_byte_argument to my.parent
    cpp_byte_argument.name = js_cpp_variable_name(my.source_argument.name)
    return cpp_byte_argument
  endnew
endfunction

function js_append_cpp_integer_argument_model(parent, source_argument)
  new cpp_integer_argument to my.parent
    cpp_integer_argument.name = js_cpp_variable_name(my.source_argument.name)
    return cpp_integer_argument
  endnew
endfunction

function js_append_cpp_unsigned_argument_model(parent, source_argument)
  new cpp_unsigned_argument to my.parent
    cpp_unsigned_argument.name = js_cpp_variable_name(my.source_argument.name)
    return cpp_unsigned_argument
  endnew
endfunction

function js_append_cpp_boolean_argument_model(parent, source_argument)
  new cpp_boolean_argument to my.parent
    cpp_boolean_argument.name = js_cpp_variable_name(my.source_argument.name)
    return cpp_boolean_argument
  endnew
endfunction

function js_append_resolved_argument(parent, source_argument, cpp_class, wrapper)
  if js_value_is_size(my.source_argument)
    js_append_cpp_size_argument_model(my.parent, my.source_argument)
  elsif js_value_is_data(my.source_argument)
    js_append_cpp_data_argument_model(my.parent, my.source_argument)
  elsif js_value_is_buffer(my.source_argument)
    js_append_cpp_buffer_argument_model(my.parent, my.source_argument, my.cpp_class, my.wrapper)
  elsif js_value_is_class_module(my.source_argument, my.wrapper)
    js_append_cpp_context_argument_model(my.parent, my.source_argument)
  elsif js_value_is_error_context(my.source_argument)
    js_append_cpp_error_context_argument_model(my.parent, my.source_argument, my.wrapper)
  elsif js_value_is_enum_module(my.source_argument, my.wrapper)
    js_append_cpp_enum_argument_model(my.parent, my.source_argument)
  elsif js_value_is_interface_module(my.source_argument, my.wrapper)
    js_append_cpp_interface_argument_model(my.parent, my.source_argument)
  elsif js_value_is_char(my.source_argument)
    js_append_cpp_char_argument_model(my.parent, my.source_argument)
  elsif js_value_is_byte(my.source_argument)
    js_append_cpp_byte_argument_model(my.parent, my.source_argument)
  elsif js_value_is_integer(my.source_argument)
    js_append_cpp_integer_argument_model(my.parent, my.source_argument)
  elsif js_value_is_unsigned(my.source_argument)
    js_append_cpp_unsigned_argument_model(my.parent, my.source_argument)
  elsif js_value_is_boolean(my.source_argument)
    js_append_cpp_boolean_argument_model(my.parent, my.source_argument)
  else
    echo_fatal("Unable to resolve argument", my)
  endif
endfunction

function js_append_cpp_js_return_model(parent, value)
  new cpp_js_return to my.parent
    cpp_js_return.name = js_cpp_variable_name(my.value.name)
    cpp_js_return.binding_name = "$(my.value.name:Camel)"
    return cpp_js_return
  endnew
endfunction

function js_append_cpp_method_return_size_model(parent, name)
  new cpp_method_return_size to my.parent
    cpp_method_return_size.name = my.name
    return cpp_method_return_size
  endnew
endfunction

function js_append_cpp_method_return_boolean_model(parent, name)
  new cpp_method_return_boolean to my.parent
    cpp_method_return_boolean.name = my.name
    return cpp_method_return_boolean
  endnew
endfunction

function js_append_cpp_method_return_data_model(parent, name)
  new cpp_method_return_data to my.parent
    cpp_method_return_data.name = my.name
    return cpp_method_return_data
  endnew
endfunction

function js_append_cpp_method_return_buffer_model(parent, name)
  new cpp_method_return_buffer to my.parent
    cpp_method_return_buffer.name = my.name
    return cpp_method_return_buffer
  endnew
endfunction

function js_append_cpp_method_return_error_model(parent, name)
  new cpp_method_return_error to my.parent
    cpp_method_return_error.name = my.name
    return cpp_method_return_error
  endnew
endfunction

function js_append_cpp_method_return_context_model(parent, name, class)
  new cpp_method_return_context to my.parent
    cpp_method_return_context.name = my.name
    cpp_method_return_context.class = my.class
    return cpp_method_return_context
  endnew
endfunction

function js_append_cpp_method_return_enum_model(parent, name, enum)
  new cpp_method_return_enum to my.parent
    cpp_method_return_enum.name = my.name
    cpp_method_return_enum.enum = my.enum
    return cpp_method_return_enum
  endnew
endfunction

function js_append_cpp_method_return_implementation_model(parent, name, implementation)
  new cpp_method_return_implementation to my.parent
    cpp_method_return_implementation.name = my.name
    cpp_method_return_implementation.implementation_name = my.implementation
  endnew
endfunction

function js_append_cpp_method_return_interface_model(parent, name, interface)
  new cpp_method_return_interface to my.parent
    cpp_method_return_interface.name = my.name
    cpp_method_return_interface.interface_name = my.interface
  endnew
endfunction

function js_append_cpp_method_return_integer_model(parent, name)
  new cpp_method_return_integer to my.parent
    cpp_method_return_integer.name = my.name
  endnew
endfunction

function js_append_cpp_method_return_unsigned_model(parent, name)
  new cpp_method_return_unsigned to my.parent
    cpp_method_return_unsigned.name = my.name
  endnew
endfunction

function js_append_cpp_method_return_byte_model(parent, name)
  new cpp_method_return_byte to my.parent
    cpp_method_return_byte.name = my.name
  endnew
endfunction

function js_append_cpp_method_return_model(parent)
  new cpp_method_return to my.parent
    cpp_method_return.name = global.js_cpp_method_return_name
    return cpp_method_return
  endnew
endfunction

function js_append_resolved_return(parent, source_return, wrapper)
  my.cpp_method_return = js_append_cpp_method_return_model(my.parent)
  if js_value_is_size(my.source_return)
    js_append_cpp_method_return_size_model(my.parent, my.cpp_method_return.name)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  elsif js_value_is_boolean(my.source_return)
    js_append_cpp_method_return_boolean_model(my.parent, my.cpp_method_return.name)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  elsif js_value_is_data(my.source_return)
    js_append_cpp_method_return_data_model(my.parent, my.cpp_method_return.name)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  elsif js_value_is_buffer(my.source_return)
    js_append_cpp_method_return_buffer_model(my.parent, my.cpp_method_return.name)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  elsif js_value_is_error(my.source_return)
    js_append_cpp_method_return_error_model(my.parent, my.cpp_method_return.name)
  elsif js_value_is_class_module(my.source_return, my.wrapper)
    js_append_cpp_method_return_context_model(my.parent, my.cpp_method_return.name, my.source_return.class)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  elsif js_value_is_enum_module(my.source_return, my.wrapper)
    js_append_cpp_method_return_enum_model(my.parent, my.cpp_method_return.name, my.source_return.enum)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  elsif js_value_is_implementation_module(my.source_return, my.wrapper)
    js_append_cpp_method_return_implementation_model(my.parent, my.cpp_method_return.name, my.source_return.impl)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  elsif js_value_is_interface_module(my.source_return, my.wrapper)
    js_append_cpp_method_return_interface_model(my.parent, my.cpp_method_return.name, my.source_return.interface)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  elsif js_value_is_integer(my.source_return)
    js_append_cpp_method_return_integer_model(my.parent, my.cpp_method_return.name)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  elsif js_value_is_unsigned(my.source_return)
    js_append_cpp_method_return_unsigned_model(my.parent, my.cpp_method_return.name)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  elsif js_value_is_byte(my.source_return)
    js_append_cpp_method_return_byte_model(my.parent, my.cpp_method_return.name)
    js_append_cpp_js_return_model(my.parent, my.cpp_method_return)
  else
    echo_fatal("Unable to resolve return", my)
  endif
endfunction

function js_append_cpp_method_model(parent, source_method, cpp_class, wrapper)
  new cpp_method to my.parent
    cpp_method.name = "$(my.source_method.name:Pascal)"
    cpp_method.binding_name = js_binding_method_name(my.source_method)
    cpp_method.c_name = "$(my.wrapper.prefix:no)_$(my.cpp_class.c_name:no)_$(my.source_method.name:c)"
    if defined(my.source_method.is_static)
      cpp_method.is_static = my.source_method.is_static
    endif
    for my.source_method.argument
      js_append_cpp_call_argument_model(cpp_method, argument)
      copy argument to cpp_method
    endfor
    for my.source_method.argument where js_cpp_value_is_cpp_js_return(argument)
      js_append_cpp_js_return_model(cpp_method, argument)
    endfor
    if defined(my.source_method->return)
      copy my.source_method->return to cpp_method
    endif
    return cpp_method
  endnew
endfunction

function js_append_cpp_class_model(parent, source, wrapper)
  new cpp_class to my.parent
    cpp_class.name = "$(my.source.name:Pascal)"
    cpp_class.binding_name = js_binding_class_name(cpp_class)
    cpp_class.c_name = "$(my.source.name:c)"
    cpp_class.context_name = "$(my.wrapper.prefix:no)_$(my.source.name:c)"
    for my.source.method where !js_method_is_private(method)
      js_append_cpp_method_model(cpp_class, method, cpp_class, my.wrapper)
    endfor
    return cpp_class
  endnew
endfunction

endtemplate
