.template 0
#   Copyright (C) 2015-2018 Virgil Security Inc.
#
#   All rights reserved.
#
#   Redistribution and use in source and binary forms, with or without
#   modification, are permitted provided that the following conditions are
#   met:
#
#       (1) Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#
#       (2) Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in
#       the documentation and/or other materials provided with the
#       distribution.
#
#       (3) Neither the name of the copyright holder nor the names of its
#       contributors may be used to endorse or promote products derived from
#       this software without specific prior written permission.
#
#   THIS SOFTWARE IS PROVIDED BY THE AUTHOR ''AS IS'' AND ANY EXPRESS OR
#   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
#   WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
#   DISCLAIMED. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT,
#   INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
#   (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
#   SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
#   HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
#   STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
#   IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
#   POSSIBILITY OF SUCH DAMAGE.
#
#   Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>

# ---------------------------------------------------------------------------
#   This module contains functions processing component 'implementation'.
#   Private function prefix is: P14.
# ---------------------------------------------------------------------------
#   This is a code generator built using the iMatix GSL code generation
#   language. See https://github.com/imatix/gsl for details.
# ---------------------------------------------------------------------------


gsl from "common.gsl"
gsl from "interface.gsl"
gsl from "component.gsl"
gsl from "c_component.gsl"

# ---------------------------------------------------------------------------
#   Check that all interfaces are implemented explicitly.
# ---------------------------------------------------------------------------
function implementation_check_interface_hierarchy (interface, implementation, source)
    check_arguments (my, "interface, implementation, source", my)

    my.interface_definition = interface_find (my.interface.name, my.source)

    for my.interface_definition.inherit
        if count (my.implementation.interface, name = inherit.interface) = 0
            echo_fatal ("Implementation of the '$(inherit.interface:)' interface \
                    required by interface '$(my.interface.name)' \
                    within <$(name (my.implementation)) name=\"$(my.implementation.name)\"/>")
        endif

        my.inherited_interface = interface_find (inherit.interface, my.source)
        implementation_check_interface_hierarchy (my.inherited_interface, my.implementation, my.source)
    endfor
endfunction

# ---------------------------------------------------------------------------
#   Copy interface methods to the implemented interface.
# ---------------------------------------------------------------------------
function implementation_copy_interface_methods (interface, implementation, source)
    check_arguments (my, "interface, implementation, source", my)

    my.interface_definition = interface_find (my.interface.name, my.source)

    for my.interface_definition.method
        copy method to my.interface
    endfor
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.interface.context.
# ---------------------------------------------------------------------------
function implementation_resolve_interface_context (context, parent)
    check_arguments (my, "context, parent", my)

    component_require_attr_name (my.context, my)
    component_resolve_instance (my.context, my.parent)
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.interface.constant.
# ---------------------------------------------------------------------------
function implementation_resolve_interface_constant (constant, parent)
    check_arguments (my, "constant, parent", my)

    component_require_attr_name (my.constant)
    component_require_attr_value (my.constant)
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.interface.
# ---------------------------------------------------------------------------
function implementation_resolve_interface (interface, parent)
    check_arguments (my, "interface, parent", my)

    component_require_attr_name (my.interface, my)

    for my.interface.context
        implementation_resolve_interface_context (context, my.interface)
    endfor

    for my.interface.constant
        implementation_resolve_interface_constant (constant, my.interface)
    endfor
endfunction

# ---------------------------------------------------------------------------
#   Resolve attribute: implementation.dependency.type.
# ---------------------------------------------------------------------------
function implementation_resolve_attr_dependency_type (require, parent)
    check_arguments (my, "require", my)

    my.require.type ?= "api"

    my.valid_types = "api, impl"

    if ! string_in (my.require.type, my.valid_types)
        my.require_dump = "<$(name (my.require)) name=\"$(my.require.name:)\" type=\"$(my.require.type)\")"

        echo_fatal ("Invalid attribute dependency.type '$(my.require_dump:)'. Expected {$(my.valid_types)}", my)
    endif
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.dependency.
# ---------------------------------------------------------------------------
function implementation_resolve_dependency (dependency, parent)
    check_arguments (my, "dependency, parent", my)

    c_component_require_attr_name (my.dependency, my)
    implementation_resolve_attr_dependency_type (my.dependency, my.parent)
endfunction

# ---------------------------------------------------------------------------
#   Resolve attribute: implementation.require_include.type.
# ---------------------------------------------------------------------------
function implementation_resolve_attr_require_include_type (require, parent)
    check_arguments (my, "require", my)

    my.require.type ?= "none"

    my.valid_types = "none, context"

    if ! string_in (my.require.type, my.valid_types)
        my.require_dump = "<$(name (my.require)) name=\"$(my.require.name:)\" type=\"$(my.require.type)\")"

        echo_fatal ("Invalid attribute require_include.type '$(my.require_dump:)'. Expected {$(my.valid_types)}", my)
    endif
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.require.
# ---------------------------------------------------------------------------
function implementation_resolve_require_include (require, parent)
    check_arguments (my, "require, parent", my)

    component_require_attr_name (my.require)
    implementation_resolve_attr_require_include_type (my.require, my.parent)
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.macros.
# ---------------------------------------------------------------------------
function implementation_resolve_macros (macros, parent)
    check_arguments (my, "macros, parent", my)

    my.macros.definition ?= "private"
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.macros.
# ---------------------------------------------------------------------------
function implementation_resolve_macroses (macroses, parent)
    check_arguments (my, "macroses, parent", my)

    my.macroses.definition ?= "private"
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.constant.
# ---------------------------------------------------------------------------
function implementation_resolve_constant (constant, parent)
    check_arguments (my, "constant, parent", my)

    my.constant.definition ?= "private"
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.enum.
# ---------------------------------------------------------------------------
function implementation_resolve_enum (enum, parent)
    check_arguments (my, "enum, parent", my)

    my.enum.declaration ?= "private"
    my.enum.definition ?= "private"
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.method.
# ---------------------------------------------------------------------------
function implementation_resolve_method (method, parent)
    check_arguments (my, "method, parent", my)

    my.method.declaration ?= "private"
    my.method.definition ?= "private"
endfunction

# ---------------------------------------------------------------------------
#   Resolve model: implementation.
# ---------------------------------------------------------------------------
function implementation_resolve (implementation, parent, project)
    check_arguments (my, "implementation, parent, project", my)

    component_require_attr_name (my.implementation, my)

    echo_trace ("Resolving implementation: $(my.implementation.name:)", my)

    for my.implementation.c_include
        implementation_resolve_c_include (c_include, my.implementation)
    endfor

    for my.implementation.interface as implemented_interface
        implementation_resolve_interface (implemented_interface, my.implementation)
        implementation_check_interface_hierarchy (implemented_interface, my.implementation, my.parent)
        implementation_copy_interface_methods (implemented_interface, my.implementation, my.parent)
    else
        echo_fatal ("Implementation '$(my.implementation.name)' does not implement any interface.", my)
    endfor

    for my.implementation.require_include
        implementation_resolve_require_include (require_include, my.implementation)
    endfor

    for my.implementation.dependency
        implementation_resolve_dependency (dependency, my.implementation)
    endfor

    for my.implementation.constant
        implementation_resolve_constant (constant, my.implementation)
    endfor

    for my.implementation.macros
        implementation_resolve_macros (macros, my.implementation)
    endfor

    for my.implementation.macroses
        implementation_resolve_macroses (macroses, my.implementation)
    endfor

    for my.implementation.enum
        implementation_resolve_enum (enum, my.implementation)
    endfor

    for my.implementation.method
        implementation_resolve_method (method, my.implementation)
    endfor

endfunction

# ---------------------------------------------------------------------------
#   Traverse implementations within source and perform complete resolution.
# ---------------------------------------------------------------------------
function foreach_implementation_resolve (source, project)
    check_arguments (my, "source, project", my)

    for my.source.implementation
        implementation_resolve (implementation, my.source, my.project)
    endfor
endfunction

# ---------------------------------------------------------------------------
#   Create 'meta entity' for each implementation within source.
# ---------------------------------------------------------------------------
function foreach_implementation_update_meta (source, meta)
    check_arguments (my, "source, meta", my)

    for my.source.implementation
        meta_add_implementation (my.meta, implementation)
    endfor
endfunction

# ---------------------------------------------------------------------------
#   Create 'feature' for given 'implementation'.
# ---------------------------------------------------------------------------
function implementation_create_feature (implementation, destination)
    check_arguments (my, "implementation, destination", my)

    new feature to my.destination
        .name = my.implementation.name

        for my.implementation.require where defined (require.project) as impl_require
            new require to feature
                .project = impl_require.project
                .feature = impl_require.feature
            endnew
        endfor

        for my.implementation.require where defined (require.library) as impl_require
            new require to feature
                .library = impl_require.library
                .feature = impl_require.feature
            endnew
        endfor

        for my.implementation.dependency
            new require to feature
                .feature = dependency.interface
            endnew
        endfor
    endnew
endfunction

# ---------------------------------------------------------------------------
#   For each 'implementation' within 'source' create 'feature'.
# ---------------------------------------------------------------------------
function foreach_implementation_create_feature (source, destination)
    check_arguments (my, "source, destination", my)

    for my.source.implementation
        implementation_create_feature (implementation, my.destination)
    endfor
endfunction

.endtemplate
