.template 0
#   Copyright (C) 2015-2019 Virgil Security, Inc.
#
#   All rights reserved.
#
#   Redistribution and use in source and binary forms, with or without
#   modification, are permitted provided that the following conditions are
#   met:
#
#       (1) Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#
#       (2) Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in
#       the documentation and/or other materials provided with the
#       distribution.
#
#       (3) Neither the name of the copyright holder nor the names of its
#       contributors may be used to endorse or promote products derived from
#       this software without specific prior written permission.
#
#   THIS SOFTWARE IS PROVIDED BY THE AUTHOR ''AS IS'' AND ANY EXPRESS OR
#   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
#   WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
#   DISCLAIMED. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT,
#   INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
#   (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
#   SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
#   HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
#   STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
#   IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
#   POSSIBILITY OF SUCH DAMAGE.
#
#   Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>

# ---------------------------------------------------------------------------
#   Contains API for PHP wrapper generation.
# ---------------------------------------------------------------------------
#   This is a code generator built using the iMatix GSL code generation
#   language. See https://github.com/imatix/gsl for details.
# ---------------------------------------------------------------------------

gsl from "common.gsl"
gsl from "interface.gsl"
gsl from "component.gsl"
gsl from "c_component.gsl"
gsl from "php_codegen.gsl"
gsl from "php_derive.gsl"
gsl from "php_check.gsl"
gsl from "php_map.gsl"
gsl from "php_resolve.gsl"
gsl from "php_format.gsl"

# ---------------------------------------------------------------------------
#   
# ---------------------------------------------------------------------------
function php_create_toplevel_modules(wrapper)
endfunction

# ---------------------------------------------------------------------------
#   
# ---------------------------------------------------------------------------
function php_create_project_module (project, wrapper)
    check_arguments (my, "project, wrapper", my)

    echo_info ("Create PHP project for project: '$(my.project.name:)'", my)

    new php_project to my.wrapper
        . = my.project. ?
        .name = my.project.name
        .description = my.project.brief
        .prefix = my.project.prefix
        .version = my.project.version_string
        .main_project = my.wrapper.project
        .project = "$(my.project.name:Pascal)"
        .project0 = my.project.name
        .source_dir = cat_path (my.wrapper.dir, php_project.main_project, php_project.project)

        copy my.project->license to php_project

        new php_uid to php_project
            .name = "$(my.project.namespace:c)"
        endnew

        if directory.create (php_project.source_dir) = -1
            echo_fatal ("Can not create directory: $(.source_dir:). ", my)
        endif

        return php_project
    endnew
endfunction

# ---------------------------------------------------------------------------
#   
# ---------------------------------------------------------------------------
function php_create_class_module (class, php_project, meta)
    check_arguments (my, "class, php_project, meta", my)

    echo_info ("Create <php_module/> from class: '$(my.class.name:)'", my)
        
    if my.class.scope <> "public" | my.class.name = "error" | my.class.context = "none"
        return
    endif

    my._type = "class"

    new php_module to my.php_project
        .name = "$(my.class.name:Pascal)"
        .name0 = "$(my.class.name:c)"
        .source_file_name = 0.name + ".php"
        .source_dir = cat_path (my.php_project.source_dir, "src")
        .source_file_path = cat_path (0.source_dir, 0.source_file_name)
        .prefix = my.php_project.prefix
        .type = my._type

        new php_license to php_module
            . = php_format_comment (my.php_project->license.)
        endnew

        new high_level to php_module
            php_map_high_level(my.class, my.php_project, high_level, my._type)
        endnew

        new low_level to php_module
            php_map_low_level(my.class, my.php_project, low_level, my._type)
        endnew
    endnew
endfunction

# ---------------------------------------------------------------------------
#   
# ---------------------------------------------------------------------------
function php_create_interface_module (interface, php_project, meta)
    check_arguments (my, "interface, php_project, meta", my)

    echo_info ("Create <php_module/> from interface: '$(my.interface.name:)'", my)

    my._type = "interface"

    new php_module to my.php_project
        .name = "$(my.interface.name:Pascal)"
        .name0 = "$(my.interface.name:c)"
        .source_file_name = 0.name + ".php"
        .source_dir = cat_path (my.php_project.source_dir, "src")
        .source_file_path = cat_path (0.source_dir, 0.source_file_name)
        .prefix = my.php_project.prefix
        .type = my._type

        new php_license to php_module
            . = php_format_comment (my.php_project->license)
        endnew

        new high_level to php_module
            php_map_high_level(my.interface, my.php_project, high_level, my._type)
        endnew

        new low_level to php_module
            .temp_name = "temp_name"
        endnew
        
    endnew
endfunction

# ---------------------------------------------------------------------------
#   
# ---------------------------------------------------------------------------
function php_create_implementation_module (implementation, php_project, meta)
    check_arguments (my, "implementation, php_project, meta", my)

    echo_info("Create <php_module/> from implementation: '$(my.implementation.name:)'", my)

    my._type = "implementation"

    new php_module to my.php_project
        .name = "$(my.implementation.name:Pascal)"
        .name0 = "$(my.implementation.name:c)"
        .source_file_name = 0.name + ".php"
        .source_dir = cat_path (my.php_project.source_dir, "src")
        .source_file_path = cat_path (0.source_dir, 0.source_file_name)
        .prefix = my.php_project.prefix
        .type = my._type

        new php_license to php_module
            . = php_format_comment (my.php_project->license)
        endnew

        new high_level to php_module
            php_map_high_level(my.implementation, my.php_project, high_level, my._type)
        endnew

        new low_level to php_module
            .temp_name = "temp_name"
        endnew
    endnew
endfunction

# ---------------------------------------------------------------------------
#   
# ---------------------------------------------------------------------------
function php_create_enum_module (enum, php_project, meta)
    check_arguments (my, "enum, php_project, meta", my)

    echo_info ("Create <php_module/> from enum: '$(my.enum.name:)'", my)

    if my.enum.scope <> "public"
        return
    endif

    if my.enum.name = "status"
        new php_module to my.php_project
            .name = "extension_status"
            .name0 = 0.name
            .filename = "status"
            .source_file_name = 0.filename + ".c"
            .source_dir = cat_path (my.php_project.source_dir, "extension")
            .source_file_path = cat_path (0.source_dir, 0.source_file_name)
            .type = "extension"

            new extension to php_module
                .prefix = my.php_project.prefix

                new code to extension
                    .= php_format_low_level_status_exception (my.enum, my.php_project.prefix)
                endnew
            endnew
        endnew
    else
        new php_module to my.php_project
            .name = "$(my.enum.name:Pascal)"
            .name0 = "$(0.name:c)"
            .source_file_name = 0.name + ".php"
            .source_dir = cat_path (my.php_project.source_dir, "src")
            .source_file_path = cat_path (0.source_dir, 0.source_file_name)
            .type = "enum"

            new php_license to php_module
                . = php_format_comment (my.php_project->license)
            endnew

            new high_level to php_module
                php_map_high_level_enum(my.enum, my.php_project, high_level)
            endnew

            new low_level to php_module
                .temp_name = "temp_name"
            endnew
        endnew
    endif
endfunction

# ---------------------------------------------------------------------------
#   
# ---------------------------------------------------------------------------
function php_create_custom_modules (source, php_project, meta)
check_arguments (my, "source, php_project, meta", my)

    if directory.create (my.php_project.source_dir) = -1
        echo_fatal ("Can not create directory: $(.source_dir:). ", my)
    endif

    new json_module to my.php_project
        .name = "composer"
        .name0 = 0.name
        .source_file_name = 0.name + ".json"
        .source_dir = cat_path (my.php_project.source_dir)
        .source_file_path = cat_path (0.source_dir, 0.source_file_name)

        new code to json_module
            .= format_left("\
            {
                \"name\": \"virgil/crypto-$(my.php_project.name:)\",
                \"description\": \"$(my.php_project.description:)\",
                \"keywords\": [],
                \"homepage\": \"https://virgilsecurity.com\",
                \"license\": \"BSD-3-Clause\",
                \"authors\": [
                    {
                        \"name\": \"Virgil Security, Inc\",
                        \"email\": \"support@virgilsecurity.com\",
                        \"homepage\": \"https://virgilsecurity.com\"
                    }
                ],
                \"require\": {
                    \"php\": \"^7.2\",
                    \"myclabs/php-enum\": \"^1.7\"
                },
                \"autoload\": {
                    \"psr-4\": {
                        \"VirgilCrypto\\\\$(my.php_project.name:Pascal)\\\\\": \"src\"
                    }
                },
                \"scripts\": {
                    \"setup-extension\": [
                    ]
                }
            }
            ", 0)
        endnew
    endnew

    new php_module to my.php_project
        .name = "extension_main"
        .name0 = 0.name
        .filename = "extension"
        .source_file_name = 0.filename + ".c"
        .source_dir = cat_path (my.php_project.source_dir, "extension")
        .source_file_path = cat_path (0.source_dir, 0.source_file_name)
        .version = my.php_project.version
        # TODO
        .extension_name = "TEMP_EXTENSION_NAME"
        .type = "extension"

        copy my.php_project->license to php_module        
    endnew
endfunction

# ---------------------------------------------------------------------------
#   
# ---------------------------------------------------------------------------
function php_finalize_custom_modules (source, destination, wrapper, meta) #[optional]
endfunction

.endtemplate
