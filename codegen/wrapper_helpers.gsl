.template 0
#   Copyright (C) 2015-2019 Virgil Security, Inc.
#
#   All rights reserved.
#
#   Redistribution and use in source and binary forms, with or without
#   modification, are permitted provided that the following conditions are
#   met:
#
#       (1) Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#
#       (2) Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in
#       the documentation and/or other materials provided with the
#       distribution.
#
#       (3) Neither the name of the copyright holder nor the names of its
#       contributors may be used to endorse or promote products derived from
#       this software without specific prior written permission.
#
#   THIS SOFTWARE IS PROVIDED BY THE AUTHOR ''AS IS'' AND ANY EXPRESS OR
#   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
#   WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
#   DISCLAIMED. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT,
#   INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
#   (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
#   SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
#   HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
#   STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
#   IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
#   POSSIBILITY OF SUCH DAMAGE.
#
#   Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>


gsl from "common.gsl"

# ===========================================================================
#   Generic helpers.
# ===========================================================================

# ---------------------------------------------------------------------------
#   Return true, if given class constant should be wrapped.
# ---------------------------------------------------------------------------
function wrapper_should_wrap_class_constant(constant)
    check_arguments(my, "constant", my)

    return my.constant.scope = "public" & my.constant.definition = "public"
endfunction

# ---------------------------------------------------------------------------
#   Return true, if given method should be wrapped.
# ---------------------------------------------------------------------------
function wrapper_should_wrap_method(method)
    check_arguments(my, "method", my)

    return my.method.scope = "public" & my.method.declaration = "public" & my.method.visibility = "public"
endfunction

# ---------------------------------------------------------------------------
#   Return true, if given instance refers to a status code.
# ---------------------------------------------------------------------------
function wrapper_instance_is_status(instance)
    check_arguments(my, "instance", my)

    return string_equal(my.instance.enum, "status")
endfunction

# ---------------------------------------------------------------------------
#   Return true, if method return status code.
# ---------------------------------------------------------------------------
function wrapper_is_method_return_status(method)
    check_arguments(my, "method", my)

    return count(my.method.return, string_equal(return.enum, "status"))
endfunction

# ---------------------------------------------------------------------------
#   Return true, if given method argument should be skipped from the wrapper.
# ---------------------------------------------------------------------------
function wrapper_method_should_skip_argument(argument)
    check_arguments(my, "argument", my)

    return my.argument.access = "writeonly" | string_equal(my.argument.class, "error")
endfunction

# ===========================================================================
#   Type helpers.
# ===========================================================================

# ---------------------------------------------------------------------------
#   Store type meta information of given instance within wrapper instance.
# ---------------------------------------------------------------------------
function wrapper_map_instance_type_info(instance, wrapper_instance)
    check_arguments(my, "instance, wrapper_instance", my)

    if defined(my.instance.type)
        my.wrapper_instance.is_primitive_type = "1"

    elsif defined(my.instance.class)
        my.wrapper_instance.is_class = "1"

        if my.instance.class = "any"
            my.wrapper_instance.is_any = "1"

        elsif my.instance.class = "data"
            my.wrapper_instance.is_data = "1"

        elsif my.instance.class = "buffer"
            my.wrapper_instance.is_buffer = "1"

        elsif my.instance.class = "error"
            my.wrapper_instance.is_error = "1"
            my.wrapper_instance.method_reset = make_ref_c(make_id("class error method reset"))
        endif

    elsif defined(my.instance.interface)
        my.wrapper_instance.is_interface = "1"

    elsif defined(my.instance.enum)
        my.wrapper_instance.is_enum = "1"

    elsif defined(my.instance.impl)
        my.wrapper_instance.is_class = "1"
    endif

    my.wrapper_instance.is_array = count(my.instance.array) ?? "1" ?
    my.wrapper_instance.is_string = count(my.instance.string) ?? "1" ?
    my.wrapper_instance.is_reference = my.instance.is_reference ?
endfunction

# ===========================================================================
#   Derive C names.
# ===========================================================================

# ---------------------------------------------------------------------------
#   Return .(UID) that refers to the C class constructor.
# ---------------------------------------------------------------------------
function class_derive_c_constructor(class, name)
    check_arguments(my, "class", my)

    return make_ref_c(make_id("class", my.class.name, "method new", my.name))
endfunction

# ---------------------------------------------------------------------------
#   Return .(UID) that refers to the C class destructor.
# ---------------------------------------------------------------------------
function class_derive_c_destructor(class)
    check_arguments(my, "class", my)

    return make_ref_c(make_id("class", my.class.name, "method delete"))
endfunction

# ---------------------------------------------------------------------------
#   Return .(UID) that refers to the C class method.
# ---------------------------------------------------------------------------
function class_derive_c_method(class, method)
    check_arguments(my, "class, method", my)

    if string_equal(my.method.of_class, "global") | string_equal (my.class.of_class, "global")
        return make_ref_c(make_id("global method", my.method.name))

    else
        my.of_class = my.method.of_class ? my.class.of_class ? my.class.name
        return make_ref_c(make_id("class", my.of_class, "method", my.method.name))
    endif
endfunction

# ---------------------------------------------------------------------------
#   Return .(UID) that refers to the C class shallow copy method.
# ---------------------------------------------------------------------------
function class_derive_c_method_shallow_copy(class)
    check_arguments(my, "class", my)

    my.of_class = my.class.of_class ? my.class.name
    return make_ref_c(make_id("class", my.of_class, "method", "shallow copy"))
endfunction

# ---------------------------------------------------------------------------
#   Return .(UID) that refers to the C interface method that returns API.
# ---------------------------------------------------------------------------
function class_derive_c_method_use_dependency(class, dependency)
    check_arguments(my, "class, dependency", my)

    return make_ref_c(make_id("class", my.class.name, "method", "use", my.dependency.name))
endfunction

# ---------------------------------------------------------------------------
#   Return .(UID) that refers to the C interface method that returns API.
# ---------------------------------------------------------------------------
function class_derive_c_method_release_dependency(class, dependency)
    check_arguments(my, "class, dependency", my)

    return make_ref_c(make_id("class", my.class.name, "method", "release", my.dependency.name))
endfunction

# ===========================================================================
#   Formatters.
# ===========================================================================

# ---------------------------------------------------------------------------
#   Return error message that is extracted from the constant description.
# ---------------------------------------------------------------------------
function enum_constant_format_oneline_description(constant)
    check_arguments(my, "constant", my)

    my.message = my.constant. ? ""

    return string_to_singleline(my.message)
endfunction

.endtemplate
